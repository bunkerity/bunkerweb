##! Please refer to the documentation at
##! https://coreruleset.org/docs/development/regex_assembly/.

##!+ i

##!> define open-comment (?:/\*)
##!> define quotes [\"'`]
##!> define quotes-plus-digits [\d\"'`]
##!> define comment-suffix (?:--|#|{{open-comment}}|\{)
##!    PB: Should comment-suffix also include "\*/" ?

##!> define logical-operators or|xor|and
##!> define math-operators div
##!> define comparison-operators like|between
##!> define math-symbols [+<>=(),-]

{{open-comment}}+{{quotes}}+\s?{{comment-suffix}}?

##!> assemble
  {{quotes}}\s*
  ##!=>
  ##! logical operators
  or
  xor
  and

  ##! math operators
  div

  ##! comparison operators
  like
  between
  ##!=>
  [\w\s-]+{{math-symbols}}\s*{{quotes-plus-digits}}
  ##!=>
##!<


##!> assemble
  {{quotes}}
  ##!=>
  [<>~]+
  \s*[^\w\s]?=\s*
  \W*?[+=]+\W*?
  ##!=>
  {{quotes}}
##!<

##!> assemble
  {{quotes}}
  ##!=>
  \s*[!=|][\d\s!=+-]+.*?[\"'`(].*?$
  \s*[!=|][\d\s!=]+.*?\d+$
  \s*(?:like|print)\W+[\w\"'`(]
  \s*;
##!<

\d{{quotes}}\s+{{quotes}}\s+\d
^admin\s*?{{quotes}}
[\"'`\(\s]\s*?glob\W+[\w\"'`(]
\sis\s*?0\W
where\s[\s\w\.,-]+\s=
